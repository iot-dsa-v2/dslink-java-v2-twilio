apply plugin: 'application'
apply plugin: 'java'

// Version is taken from dslink.json
build {
    version = new groovy.json.JsonSlurper().parseText(file('dslink.json').text).version
}

mainClassName = 'org.iot.dsa.dslink.DSLink'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenLocal()
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    //implementation 'org.iot-dsa:dslink-v2-websocket:+' //for a locally installed sdk
    implementation 'com.github.iot-dsa-v2.sdk-dslink-java-v2:dslink-v2-websocket:0.70.0'
    implementation 'org.apache.cxf:cxf-rt-rs-client:3.0.0-milestone1'
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name' : 'org.iot.dsa.dslink.twilio'
        )
    }
}

applicationDistribution.from(new File(project.projectDir, "/dslink.json"))

run {
    args System.getProperty("exec.args", "").split()
    workingDir project.buildDir
}

distZip {
    archiveName project.name + ".zip"
    eachFile { file ->
        String path = file.relativePath
        file.setPath(path.substring(path.indexOf("/")+1,path.length()))
    }
}

distTar.enabled = false


wrapper {
    gradleVersion = '5.6.2'
}
